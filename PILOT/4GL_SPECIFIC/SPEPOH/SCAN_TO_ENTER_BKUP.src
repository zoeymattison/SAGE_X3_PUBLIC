$ACTION
Case ACTION
    When "AFFMASK" : Gosub AFFMASK
    When "ABANDON" : Gosub ABANDON
    When "STYLE" : Gosub STYLE
    When "SETBOUT" : Gosub SETBOUT
    When Default
Endcase
Return

# Disable the new fields when opening the window

$AFFMASK
Diszo [M:POH2]ZSCNLN
Diszo [M:POH2]ZSCNQY
Return

# Disable the new fields when cancelling creation of a new PO

$ABANDON
Diszo [M:POH2]ZSCNLN
Diszo [M:POH2]ZSCNQY
Return

# Specific button processing in the SETBOUT action when in entry transaction RET. If the user is not using the RET entry transaction,
# the button to enable the scan-to-enter function will be greyed out.
# 1. Grey-out the PO number and order date fields
# 2. Case the supplier code. If it returns intersite = 2, proceed to enable to scan-to-enter button in tag $SCAN_TO_ENTER
# 3. Processing for the scan-to-enter button Za, enabling modification mode (REP=”M”) and enabling usage of the ZSCNLN field
# 4. Prevent clicking of the button Za if we're already in a modification mode

$SETBOUT
If GFLAG = "RET"
    Diszo [M:POH0]POHNUM
    Diszo [M:POH0]ORDDAT

    If !find(REP,"C","M","D")
        If [M:POH2]ITMREF(0) <> "" : nolign = [M:POH2]NBLIG+1 : Endif
        # We will always set the value of nolign if there is an existing item in order
        # to fix the bug with entering new lines on an existing PO.
        Gosub SCAN_TO_ENTER
    Endif
    If BOUT = "a" & !find(REP,"C","M","D")
        Actzo [M:POH2]ZSCNLN
        REP = "M"
    Endif
Endif
Return

# This tag checks the status of the PO on screen if it is not a closed PO
# 1. If the PO is not in any modification mode, then enable the “Enable scan to enter” button Za
# 2. If the PO is in modification, creation or duplication mode, enable the field ZSCNLN

$SCAN_TO_ENTER
If GFLAG = "RET"
If [F:POH]CLEFLG = 1
    If !find(REP,"C","M","D")
        CHMEN += "Za"
    Else
        Actzo [M:POH2]ZSCNLN
    Endif
Endif
Endif
Return

# Disable the custom fields when opening an existing PO

$STYLE
Diszo [M:POH2]ZSCNLN
Diszo [M:POH2]ZSCNQY
Local Integer I
       For I = 0 To [M:POH2]NBLIG - 1
        [M:POH2]YITMBPS(I)=func AFNC.INTFLD('ITMBPS','ITMREFBPS',[M:POH2]ITMREF(I)+'~'+[M:POH0]BPSNUM)
        [M:POH2]ZITMSTA(I)=func AFNC.INTFLD('ITMMASTER','TSICOD(0)',[M:POH2]ITMREF(I))
        Affzo [M:POH2]YITMBPS(I)
        Affzo [M:POH2]ZITMSTA(I)
       Next
Return

Subprog AP_ITMREF(VALEUR)
Variable Char    VALEUR()
Local Char ZSTA : ZSTA = func AFNC.INTFLD('ITMMASTER','TSICOD(0)',VALEUR)
Local Char ZSITE : ZSITE = [M:POH0]BPSNUM
If ZSITE = 'IDC30' or ZSITE = 'IDC52' or ZSITE = 'IDC33'
	ZSITE = right$([M:POH0]BPSNUM,2)
Endif
#infbox ZSITE
If func AFNC.INTFLD('ITMMVT','PHYSTO',VALEUR+'~'+[M:POH0]POHFCY) >= '0'
If ZSTA = 'DS' or ZSTA = 'DM'
	If func AFNC.INTFLD('ITMMVT','PHYSTO',VALEUR+'~'+ZSITE) >= '0'
		Infbox('Warning: '+VALEUR+' is discontinued!'+chr$(13)+chr$(13)+chr$(13)+' The available quantity in '+ZSITE+' is '+num$(func AFNC.INTFLD('ITMMVT','PHYSTO',VALEUR+'~'+ZSITE)))
	Else Infbox('Warning: '+VALEUR+' is discontinued!')
	Endif
	If pat([M:POH0]BPSNUM,'V*')<>0
		Infbox('Warning: '+VALEUR+' is discontinued!')
	Endif
Endif
Endif
[M:POH2]YITMBPS(nolign-1)=func AFNC.INTFLD('ITMBPS','ITMREFBPS',VALEUR+'~'+[M:POH0]BPSNUM)
[M:POH2]ZITMSTA(nolign-1)=ZSTA
Affzo [M:POH2]YITMBPS(nolign-1)
Affzo [M:POH2]ZITMSTA(nolign-1)
End

Subprog AP_YEXPDAT(VALEUR)
Variable Date    VALEUR
Local Integer I : I= [M:POH2]NBLIG
Local Integer NEEDUPDATE : NEEDUPDATE = 0
Local Integer YN : YN = 2 :# By default points on Yes

If num$(VALEUR) <> "00/00/0000"
    For I = 0 To [M:POH2]NBLIG - 1
       If [M:POH2]EXTRCPDAT(I) <> VALEUR and [M:POH2]LINCLEFLG(I) < 2
         NEEDUPDATE = 1
         Break
       Endif
    Next
Endif
If NEEDUPDATE = 1
    Call OUINON("Do you want to update all lines with new expected receive date?", YN) From GESECRAN
   If YN = 2
       For I = 0 To [M:POH2]NBLIG - 1
          If [M:POH2]EXTRCPDAT(I) <> VALEUR and [M:POH2]LINCLEFLG(I) < 2
             [M:POH2]EXTRCPDAT(I) = VALEUR
             Affzo [M:POH2]EXTRCPDAT(I)
          Endif
       Next
    Endif
Endif
End

Subprog AS_EXTRCPDAT(VALEUR)
Variable Date    VALEUR
Local Integer I : I = nolign - 1
If [M:POH0]YEXPDAT <> VALEUR and num$([M:POH0]YEXPDAT) <> "00/00/0000"
    VALEUR = [M:POH0]YEXPDAT
    [M:POH2]EXTRCPDAT(I) = VALEUR
    Affzo [M:POH2]EXTRCPDAT(I)
Endif
End

Subprog AM_ZSCNLN(VALEUR)
Variable Char    VALEUR()
If GFLAG = "RET"
If func AFNC.INTFLD("ITMFACILIT","AUUID",VALEUR+"~"+[M:POH0]POHFCY) = ""
    GMESSAGE = VALEUR+" non-existent product site "+[M:POH0]POHFCY
    mkstat = 2
Else
    Actzo [M:POH2]ZSCNQY
    Diszo [M:POH2]ZSCNLN
    zonsui = "[M:POH2]ZSCNQY"
Endif
Endif
End


Subprog AM_ZSCNQY(VALEUR)
Variable Decimal VALEUR
If GFLAG = "RET"
If VALEUR > 0
    Local Shortint NOL
    Local Integer LIN : [L]LIN = 0
    For I=0 To [M:POH2]NBLIG-1
        If [M:POH2]ITMREF(I)=[M:POH2]ZSCNLN & [M:POH2]LINCLEFLG(I)=1
            [L]NOL = I
            [L]LIN+=1
        Endif
    Next
    If [L]LIN > 0
        Goto APPEND_LINE
    Else
        [L]NOL = max([M:POH2]NBLIG)
        Goto ENTER_NEWLINE
    Endif
Endif
Endif
End

$ENTER_NEWLINE
If GFLAG = "RET"
[M:POH2]ITMREF(NOL) = [M:POH2]ZSCNLN
#Call AM_ITMREF([M:POH2]ITMREF(NOL)) From SUBPOH
[M:POH2]QTYUOM(NOL) = VALEUR
#Call AM_QTYUOM([M:POH2]QTYUOM(NOL)) From SUBPOH
nolign+=1
Goto GET_POSITION
Endif
End

$APPEND_LINE
If GFLAG = "RET"
[M:POH2]QTYUOM(NOL) += VALEUR
[M:POH2]QTYSTU(NOL) += VALEUR
[M:POH2]QTYPUU(NOL) += VALEUR
#Call INI_TARIF(NOL,"QTYUOM") From SUBPOHB
Goto GET_POSITION
Endif
End

$GET_POSITION
If GFLAG = "RET"
If max([M:POH2]NBLIG) = 0
    [M:POH2]NBLIG = 1
    Goto AFFZO_MASK
Else
    If max([M:POH2]NBLIG) > [L]NOL
        Goto AFFZO_MASK
    Else
        [M:POH2]NBLIG = max([M:POH2]NBLIG)+1
        Goto AFFZO_MASK
    Endif
Endif
Endif
End

$AFFZO_MASK
If GFLAG = "RET"
Affzo [POH2]
Raz [M:POH2]ZSCNLN
Raz [M:POH2]ZSCNQY
Affzo [M:POH2]ZSCNLN
Affzo [M:POH2]ZSCNQY
Diszo [M:POH2]ZSCNQY
Actzo [M:POH2]ZSCNLN
zonsui="[M:POH2]ZSCNLN"
Endif
End

Subprog AM_BPSNUM(VALEUR)
Variable Char    VALEUR()
If GFLAG = "RET"
    If GREP = "C"
        Actzo [M:POH2]ZSCNLN
        Diszo [M:POH2]ZSCNQY
    Else
        mkstat = 2
        GMESSAGE = 'Document does not exist.\Please click "NEW" first!'
    Endif
Endif
End

Subprog C_ZSCNQY(VALEUR)
Variable Decimal VALEUR
If GFLAG = "RET"
    If VALEUR = 0
        Local Integer YN : YN = 2
        Call OUINON("Quantity must be greater than 0\Cancel the entry?", YN) From GESECRAN
        If YN = 2
            Goto AFFZO_MASK
        Else
            Raz [M:POH2]ZSCNQY
            Affzo [M:POH2]ZSCNQY
            zonsui="[M:POH2]ZSCNQY"
            mkstat=2
        Endif
    Endif
    If VALEUR > 1000
        GMESSAGE = "Maximum quantity 1000"
        Raz [M:POH2]ZSCNQY
        Affzo [M:POH2]ZSCNQY
        zonsui="[M:POH2]ZSCNQY"
        mkstat=2
    Endif
Endif
End
