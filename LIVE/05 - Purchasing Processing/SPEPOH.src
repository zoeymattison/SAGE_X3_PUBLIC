$ACTION
Case ACTION
    When "OUVRE" : Gosub OUVRE
    When "STYLE" : Gosub STYLE
    When "SETBOUT" Gosub SETBOUT
    When "VERIF_MOD" Gosub VERIF_MOD
    When "VERIF_CRE" Gosub VERIF_CRE
    When "APRES_MOD" : Gosub APRES_MOD
    When "APRES_CRE" : Gosub APRES_CRE
    When "ABANDON" : Gosub ABANDON
    When Default
Endcase
Return

$OUVRE
If GFLAG = "RET" : Global Char ZSCANNEDITEM(250) : Endif
Return

$STYLE
Gosub DISABLE_FIELDS
Local Integer I
       For I = 0 To [M:POH2]NBLIG - 1
        [M:POH2]YITMBPS(I)=func AFNC.INTFLD('ITMBPS','ITMREFBPS',[M:POH2]ITMREF(I)+'~'+[M:POH0]BPSNUM)
        [M:POH2]ZITMSTA(I)=func AFNC.INTFLD('ITMMASTER','TSICOD(0)',[M:POH2]ITMREF(I))
        Affzo [M:POH2]YITMBPS(I)
        Affzo [M:POH2]ZITMSTA(I)
       Next
Return

$SETBOUT
If BOUT = "N"
Gosub DISABLE_FIELDS
Endif
If GFLAG = "RET"
    Diszo [M:POH0]POHNUM
    Diszo [M:POH0]ORDDAT

    If !find(REP,"C","M","D")
        Gosub SCAN_TO_ENTER
    Endif
    If BOUT = "a" & !find(REP,"C","M","D")
        Actzo [M:POH0]ZSCNLN
        REP = "M"
    Endif
Endif
Return

$VERIF_MOD
If GFONCTION = 'GESPOH'
    If !clalev([ZPO]) : Local File PORDER[ZPO] : Endif
    Read [ZPO]POH0 = [M:POH0]POHNUM
    If [F:ZPO]ZPOSTA = 1 and [M:POH0]ZPOSTA = 2
        [M:ZPOH]ZPOAPU = GNOMUSER
        [M:ZPOH]ZPOAPD = date$
        Affzo [M:ZPOH]ZPOAPD
        Affzo [M:ZPOH]ZPOAPU
    Endif
Endif
Return

$VERIF_CRE
If find(val(func AFNC.INTFLD("BPSUPPLIER","ZBPSAPR",[M:POH0]BPSNUM)),0,1)
    [M:POH0]ZPOSTA = 2
Else
    [M:POH0]ZPOSTA = 1
Endif
Affzo [M:POH0]ZPOSTA

If GFONCTION = 'GESPOH'
    Raz [M:ZPOH]
    Affzo [M:ZPOH]
Endif
Return

$APRES_MOD
Gosub DISABLE_FIELDS
Return

$APRES_CRE
If [M:POH0]ZPOSTA = 2
    If !clalev([ZPO]) : Local File PORDER[ZPO] : Endif
    Update [ZPO] Where POHNUM = [M:POH0]POHNUM With ZPOAPU = GNOMUSER, ZPOAPD = date$
Endif
Return

$ABANDON
Gosub DISABLE_FIELDS
Return

$DISABLE_FIELDS
Diszo [M:POH0]ZSCNLN
Diszo [M:POH0]ZSCNQY
Return

$SCAN_TO_ENTER
If GFLAG = "RET"
If [F:POH]CLEFLG = 1
    If !find(REP,"C","M","D") & [F:POH]BETFCY = 2
        CHMEN += "Za"
    Else
        Actzo [M:POH0]ZSCNLN
    Endif
Endif
Endif
Return

Subprog AP_TSSCOD(VALEUR)
Variable Char    VALEUR()
End

Subprog AP_YEXPDAT(VALEUR)
Variable Date    VALEUR
Local Integer I : I= [M:POH2]NBLIG
Local Integer NEEDUPDATE : NEEDUPDATE = 0
Local Integer YN : YN = 2 :# By default points on Yes

If num$(VALEUR) <> "00/00/0000"
    For I = 0 To [M:POH2]NBLIG - 1
       If [M:POH2]EXTRCPDAT(I) <> VALEUR and [M:POH2]LINCLEFLG(I) < 2
         NEEDUPDATE = 1
         Break
       Endif
    Next
Endif
If NEEDUPDATE = 1
    Call OUINON("Do you want to update all lines with new expected receive date?", YN) From GESECRAN
   If YN = 2
       For I = 0 To [M:POH2]NBLIG - 1
          If [M:POH2]EXTRCPDAT(I) <> VALEUR and [M:POH2]LINCLEFLG(I) < 2
             [M:POH2]EXTRCPDAT(I) = VALEUR
             Affzo [M:POH2]EXTRCPDAT(I)
          Endif
       Next
    Endif
Endif
End

Subprog AS_EXTRCPDAT(VALEUR)
Variable Date    VALEUR
Local Integer I : I = nolign - 1
If [M:POH0]YEXPDAT <> VALEUR and num$([M:POH0]YEXPDAT) <> "00/00/0000"
    VALEUR = [M:POH0]YEXPDAT
    [M:POH2]EXTRCPDAT(I) = VALEUR
    Affzo [M:POH2]EXTRCPDAT(I)
Endif
End

Subprog AP_ITMREF(VALEUR)
Variable Char    VALEUR()
Local Char ZSITE : ZSITE = [M:POH0]BPSNUM
Local Char ZSTA : ZSTA = func AFNC.INTFLD('ITMMASTER','TSICOD(0)',VALEUR)

If ZSITE = 'IDC30' or ZSITE = 'IDC52' or ZSITE = 'IDC33'
	ZSITE = right$([M:POH0]BPSNUM,2)
Endif

If !clalev([ZITF]) : Local File ITMFACILIT[ZITF] : Endif

If func AFNC.INTFLD('ITMMVT','PHYSTO',VALEUR+'~'+[M:POH0]POHFCY) <> ''
If ZSTA = 'DS' or ZSTA = 'DM'
	If pat(ZSITE, 'V*')<>1
		Read [ZITF]ITF0 = VALEUR;ZSITE
		If fstat = 0
			 Infbox('Warning: '+VALEUR+' is discontinued! The available quantity in '+ZSITE+' is '+num$(func AFNC.INTFLD('ITMMVT','PHYSTO',VALEUR+'~'+ZSITE)))
		Else Call ERREURT(ZSITE+': No product-site found for this product!', 1) From GESECRAN : Endif
	Else Infbox('Warning: '+VALEUR+' is discontinued!') : Endif
Endif
Endif

[M:POH2]YITMBPS(nolign-1)=func AFNC.INTFLD('ITMBPS','ITMREFBPS',VALEUR+'~'+[M:POH0]BPSNUM)
[M:POH2]ZITMSTA(nolign-1)=ZSTA
Affzo [M:POH2]YITMBPS(nolign-1)
Affzo [M:POH2]ZITMSTA(nolign-1)
End

Subprog AP_ZSTOFCY(VALEUR)
Variable Char    VALEUR()
Local Integer YN : YN = 2
Local Char ZADD

If VALEUR <> '' and [M:POH0]POHNUM <> ''
    If find(VALEUR, GSITE)
        Call OUINON('Update receiving site of all available lines to '+VALEUR+'?', YN) From GESECRAN
        If !clalev([ZBP]) : Local File BPADDRESS[ZBP] : Endif
        Filter [ZBP] Where BPANUM = VALEUR and BPAADDFLG = 2
        Read [ZBP] First
        ZADD = [ZBP]BPAADD
        If YN = 2
            For I = 0 To [M:POH2]NBLIG-1
                If [M:POH2]LINCLEFLG(I) < 2 and func AFNC.INTFLD('ITMFACILIT','AUUID',[M:POH2]ITMREF(I)+'~'+VALEUR) <> '' and [M:POH2]PRHFCY(I) <> VALEUR
                    [M:POH2]PRHFCY(I) = VALEUR : Affzo [M:POH2]PRHFCY(I)
                    [M:POH2]FCYADD(I) = ZADD : Affzo [M:POH2]FCYADD(I)
                Endif
            Next
            zonsui = "[M:POH1]ORDREF"
        Else
            Raz [M:POH0]ZSTOFCY : Affzo [M:POH0]ZSTOFCY : Raz VALEUR : End
        Endif
    Endif
Raz [M:POH0]ZSTOFCY : Affzo [M:POH0]ZSTOFCY
Raz VALEUR
Endif
End

Subprog AM_ZSCNLN(VALEUR)
Variable Char    VALEUR()
If GFLAG = "RET"
    [V]ZSCANNEDITEM = VALEUR
    Actzo [M:POH0]ZSCNQY
    Diszo [M:POH0]ZSCNLN
    [M:POH0]ZSCNDES1 = func AFNC.INTFLD("ITMMASTER","ITMDES1",[V]ZSCANNEDITEM)
    [M:POH0]ZSCNDES2 = func AFNC.INTFLD("ITMMASTER","ITMDES2",[V]ZSCANNEDITEM)+" "+func AFNC.INTFLD("ITMMASTER","ITMDES3",[V]ZSCANNEDITEM)
    Affzo [M:POH0]ZSCNDES1
    Affzo [M:POH0]ZSCNDES2
    zonsui = "[M:POH0]ZSCNQY"
Endif
End

Subprog AP_ZSCNQY(VALEUR)
Variable Decimal VALEUR
If GFLAG = "RET"
If VALEUR > 0
    Gosub RAZ_DES
    Local Shortint ZNOL : [L]ZNOL = 0
    Local Integer LIN : [L]LIN = 0
    If [M:POH2]ITMREF <> ""
        For I=0 To [M:POH2]NBLIG-1
            If [M:POH2]ITMREF(I)=[V]ZSCANNEDITEM & [M:POH2]LINCLEFLG(I)=1
                [L]ZNOL = I
                [L]LIN+=1
            Endif
        Next
    Endif
    If [L]LIN > 0
        Goto APPEND_LINE
    Else
        [L]ZNOL = [M:POH2]NBLIG
        Goto ENTER_NEWLINE
    Endif
Endif
Endif
End

$RAZ_DES
Raz [M:POH0]ZSCNDES1, [M:POH0]ZSCNDES2
Affzo [M:POH0]ZSCNDES1
Affzo [M:POH0]ZSCNDES2
Return

$ENTER_NEWLINE
Default Mask [POH2]
nolign=[L]ZNOL+1
[M:POH2]ITMREF(ZNOL) = [V]ZSCANNEDITEM
Call AM_ITMREF([M:POH2]ITMREF(ZNOL)) From SUBPOH
[M:POH2]QTYUOM(ZNOL) = VALEUR
Call AM_QTYUOM([M:POH2]QTYUOM(ZNOL)) From SUBPOH
[M:POH2]ZITMSTA(ZNOL) = func AFNC.INTFLD('ITMMASTER','TSICOD(0)',[V]ZSCANNEDITEM)
nolign+=1
Goto GET_POSITION
End

$APPEND_LINE
[M:POH2]QTYUOM(ZNOL) += VALEUR
[M:POH2]QTYSTU(ZNOL) += VALEUR
[M:POH2]QTYPUU(ZNOL) += VALEUR
Call INI_TARIF(ZNOL,"QTYUOM") From SUBPOHB
Goto GET_POSITION
End

$GET_POSITION
If [M:POH2]NBLIG = 0
    [M:POH2]NBLIG = 1
    Goto AFFZO_MASK
Else
    If [M:POH2]NBLIG > [L]ZNOL
        Goto AFFZO_MASK
    Else
        [M:POH2]NBLIG+=1
        Goto AFFZO_MASK
    Endif
Endif
End

$AFFZO_MASK
Affzo [POH2]
Raz [M:POH0]ZSCNLN, [M:POH0]ZSCNQY, [L]ZNOL, [V]ZSCANNEDITEM
Affzo [M:POH0]ZSCNLN
Affzo [M:POH0]ZSCNQY
Diszo [M:POH0]ZSCNQY
Actzo [M:POH0]ZSCNLN
zonsui="[M:POH0]ZSCNLN"
End

Subprog C_BPSNUM(VALEUR)
Variable Char    VALEUR()
If func AFNC.INTFLD("BPARTNER","BETFCY",VALEUR)="1" & GFLAG = "RET"
    GMESSAGE = "Only intersite creation permitted for the RET entry transaction!"
    mkstat = 2
Endif
End

Subprog AM_BPSNUM(VALEUR)
Variable Char    VALEUR()
If GFLAG = "RET"
    If GREP = "C"
        Actzo [M:POH0]ZSCNLN
        Diszo [M:POH0]ZSCNQY
    Else
        mkstat = 2
        GMESSAGE = 'Document does not exist.\Please click "NEW" first!'
    Endif
Endif
End

Subprog C_ZSCNQY(VALEUR)
Variable Decimal VALEUR
If GFLAG = "RET"
    If VALEUR = 0
        Local Integer YN : YN = 2
        Call OUINON("Quantity must be greater than 0\Cancel the entry?", YN) From GESECRAN
        If YN = 2
            Gosub RAZ_DES
            Goto AFFZO_MASK
        Else
            Raz [M:POH0]ZSCNQY
            Affzo [M:POH0]ZSCNQY
            zonsui="[M:POH0]ZSCNQY"
            mkstat=2
        Endif
    Endif
    If VALEUR > 1000
        GMESSAGE = "Maximum quantity 1000"
        Raz [M:POH0]ZSCNQY
        Affzo [M:POH0]ZSCNQY
        zonsui="[M:POH0]ZSCNQY"
        mkstat=2
    Endif
Endif
End

Subprog C_ZSCNLN(VALEUR)
Variable Char    VALEUR()
If VALEUR = ""
    End
Endif
If func AFNC.INTFLD("ITMFACILIT","AUUID",VALEUR+"~"+[M:POH0]POHFCY) = ""
    GMESSAGE = VALEUR+" non-existent product site "+[M:POH0]POHFCY
    mkstat = 2
Endif
If (func AFNC.INTFLD("BPARTNER","BETFCY",[M:POH0]BPSNUM)="2" and
& func AFNC.INTFLD("ITMFACILIT","AUUID",VALEUR+"~"+func AFNC.INTFLD("BPARTNER","FCY",[M:POH0]BPSNUM)) = "")
    GMESSAGE = VALEUR+" non-existent product site "+func AFNC.INTFLD("BPARTNER","FCY",[M:POH0]BPSNUM)
    mkstat = 2
Endif
End



