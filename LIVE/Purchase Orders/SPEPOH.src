$ACTION
Case ACTION
    When "AFFMASK" : Gosub AFFMASK
    When "STYLE" : Gosub STYLE
    When "VERIF_CRE" Gosub VERIF_CRE
    When "VERIF_MOD" Gosub VERIF_MOD
    When "APRES_CRE" Gosub APRES_CRE
    When "SETBOUT" Gosub SETBOUT
    When "ABANDON" : Gosub ABANDON
    When Default
Endcase
Return

$AFFMASK
Diszo [M:POH0]ZSCNLN
Diszo [M:POH0]ZSCNQY
Return

$ABANDON
Diszo [M:POH0]ZSCNLN
Diszo [M:POH0]ZSCNQY
Return


$SETBOUT
If GFLAG = "RET"
    Diszo [M:POH0]POHNUM
    Diszo [M:POH0]ORDDAT
    Case num$([M:POH0]BETFCY)
        When "1" : Diszo [M:POH0]ZSCNLN : Diszo [M:POH0]ZSCNQY
        When "2" : Gosub SCAN_TO_ENTER
    Endcase

    If BOUT = "a"
        Actzo [M:POH0]ZSCNLN
        REP = "M"
    Endif
Endif
Return

$SCAN_TO_ENTER
If [F:POH]CLEFLG = 1
    If !find(REP,"C","M","D")
        CHMEN += "Za"
    Else
        Actzo [M:POH0]ZSCNLN
    Endif
Endif
Return

$STYLE
Diszo [M:POH0]ZSCNLN
Diszo [M:POH0]ZSCNQY
Local Integer I
       For I = 0 To [M:POH2]NBLIG - 1
        [M:POH2]YITMBPS(I)=func AFNC.INTFLD('ITMBPS','ITMREFBPS',[M:POH2]ITMREF(I)+'~'+[M:POH0]BPSNUM)
        [M:POH2]ZITMSTA(I)=func AFNC.INTFLD('ITMMASTER','TSICOD(0)',[M:POH2]ITMREF(I))
        Affzo [M:POH2]YITMBPS(I)
        Affzo [M:POH2]ZITMSTA(I)
       Next
Return

$APRES_CRE
If [M:POH0]ZPOSTA = 2
    If !clalev([ZPO]) : Local File PORDER[ZPO] : Endif
    Update [ZPO] Where POHNUM = [M:POH0]POHNUM With ZPOAPU = GNOMUSER, ZPOAPD = date$
Endif
Return

$VERIF_CRE
If find(val(func AFNC.INTFLD("BPSUPPLIER","ZBPSAPR",[M:POH0]BPSNUM)),0,1)
    [M:POH0]ZPOSTA = 2
Else
    [M:POH0]ZPOSTA = 1
Endif
Affzo [M:POH0]ZPOSTA

If GFONCTION = 'GESPOH'
    Raz [M:ZPOH]
    Affzo [M:ZPOH]
Endif
Return

$VERIF_MOD
If GFONCTION = 'GESPOH'
    If !clalev([ZPO]) : Local File PORDER[ZPO] : Endif
    Read [ZPO]POH0 = [M:POH0]POHNUM
    If [F:ZPO]ZPOSTA = 1 and [M:POH0]ZPOSTA = 2
        [M:ZPOH]ZPOAPU = GNOMUSER
        [M:ZPOH]ZPOAPD = date$
        Affzo [M:ZPOH]ZPOAPD
        Affzo [M:ZPOH]ZPOAPU
    Endif
Endif
Return

Subprog AP_TSSCOD(VALEUR)
Variable Char    VALEUR()
End

Subprog AP_YEXPDAT(VALEUR)
Variable Date    VALEUR
Local Integer I : I= [M:POH2]NBLIG
Local Integer NEEDUPDATE : NEEDUPDATE = 0
Local Integer YN : YN = 2 :# By default points on Yes

If num$(VALEUR) <> "00/00/0000"
    For I = 0 To [M:POH2]NBLIG - 1
       If [M:POH2]EXTRCPDAT(I) <> VALEUR and [M:POH2]LINCLEFLG(I) < 2
         NEEDUPDATE = 1
         Break
       Endif
    Next
Endif
If NEEDUPDATE = 1
    Call OUINON("Do you want to update all lines with new expected receive date?", YN) From GESECRAN
   If YN = 2
       For I = 0 To [M:POH2]NBLIG - 1
          If [M:POH2]EXTRCPDAT(I) <> VALEUR and [M:POH2]LINCLEFLG(I) < 2
             [M:POH2]EXTRCPDAT(I) = VALEUR
             Affzo [M:POH2]EXTRCPDAT(I)
          Endif
       Next
    Endif
Endif
End

Subprog AS_EXTRCPDAT(VALEUR)
Variable Date    VALEUR
Local Integer I : I = nolign - 1
If [M:POH0]YEXPDAT <> VALEUR and num$([M:POH0]YEXPDAT) <> "00/00/0000"
    VALEUR = [M:POH0]YEXPDAT
    [M:POH2]EXTRCPDAT(I) = VALEUR
    Affzo [M:POH2]EXTRCPDAT(I)
Endif
End

Subprog AP_ITMREF(VALEUR)
Variable Char    VALEUR()
Local Char ZSITE : ZSITE = [M:POH0]BPSNUM
Local Char ZSTA : ZSTA = func AFNC.INTFLD('ITMMASTER','TSICOD(0)',VALEUR)

If ZSITE = 'IDC30' or ZSITE = 'IDC52' or ZSITE = 'IDC33'
	ZSITE = right$([M:POH0]BPSNUM,2)
Endif

If !clalev([ZITF]) : Local File ITMFACILIT[ZITF] : Endif

If func AFNC.INTFLD('ITMMVT','PHYSTO',VALEUR+'~'+[M:POH0]POHFCY) <> ''
If ZSTA = 'DS' or ZSTA = 'DM'
	If pat(ZSITE, 'V*')<>1
		Read [ZITF]ITF0 = VALEUR;ZSITE
		If fstat = 0
			 Infbox('Warning: '+VALEUR+' is discontinued! The available quantity in '+ZSITE+' is '+num$(func AFNC.INTFLD('ITMMVT','PHYSTO',VALEUR+'~'+ZSITE)))
		Else Call ERREURT(ZSITE+': No product-site found for this product!', 1) From GESECRAN : Endif
	Else Infbox('Warning: '+VALEUR+' is discontinued!') : Endif
Endif
Endif

[M:POH2]YITMBPS(nolign-1)=func AFNC.INTFLD('ITMBPS','ITMREFBPS',VALEUR+'~'+[M:POH0]BPSNUM)
[M:POH2]ZITMSTA(nolign-1)=ZSTA
Affzo [M:POH2]YITMBPS(nolign-1)
Affzo [M:POH2]ZITMSTA(nolign-1)
End

Subprog AP_ZSTOFCY(VALEUR)
Variable Char    VALEUR()
Local Integer YN : YN = 2
Local Char ZADD

If VALEUR <> '' and [M:POH0]POHNUM <> ''
    If find(VALEUR, GSITE)
        Call OUINON('Update receiving site of all available lines to '+VALEUR+'?', YN) From GESECRAN
        If !clalev([ZBP]) : Local File BPADDRESS[ZBP] : Endif
        Filter [ZBP] Where BPANUM = VALEUR and BPAADDFLG = 2
        Read [ZBP] First
        ZADD = [ZBP]BPAADD
        If YN = 2
            For I = 0 To [M:POH2]NBLIG-1
                If [M:POH2]LINCLEFLG(I) < 2 and func AFNC.INTFLD('ITMFACILIT','AUUID',[M:POH2]ITMREF(I)+'~'+VALEUR) <> '' and [M:POH2]PRHFCY(I) <> VALEUR
                    [M:POH2]PRHFCY(I) = VALEUR : Affzo [M:POH2]PRHFCY(I)
                    [M:POH2]FCYADD(I) = ZADD : Affzo [M:POH2]FCYADD(I)
                Endif
            Next
            zonsui = "[M:POH1]ORDREF"
        Else
            Raz [M:POH0]ZSTOFCY : Affzo [M:POH0]ZSTOFCY : Raz VALEUR : End
        Endif
    Endif
Raz [M:POH0]ZSTOFCY : Affzo [M:POH0]ZSTOFCY
Raz VALEUR
Endif
End

Subprog AM_ZSCNLN(VALEUR)
Variable Char    VALEUR()
If func AFNC.INTFLD("ITMFACILIT","AUUID",VALEUR+"~"+[M:POH0]POHFCY) = ""
    GMESSAGE = VALEUR+" non-existant product site "+[M:POH0]POHFCY
    mkstat = 2
Else
    Actzo [M:POH0]ZSCNQY
    Diszo [M:POH0]ZSCNLN
    zonsui = "[M:POH0]ZSCNQY"
Endif
End

Subprog AP_ZSCNQY(VALEUR)
Variable Decimal VALEUR
If VALEUR > 0
    Local Shortint NOL
    Local Integer LIN : [L]LIN = 0
    For I = 0 To [M:POH2]NBLIG-1
        If [M:POH2]ITMREF(I) = [M:POH0]ZSCNLN & [M:POH2]LINCLEFLG(I) = 1
            [L]NOL = I
            [L]LIN += 1
            Break
        Endif
    Next
    If [L]LIN > 0
        Goto APPEND_LINE
    Else
        [L]NOL = max([M:POH2]NBLIG)
        Goto ENTER_NEWLINE
    Endif
Endif
End

$ENTER_NEWLINE
[M:POH2]ITMREF(NOL) = [M:POH0]ZSCNLN
[M:POH2]ITMDES(NOL) = func AFNC.INTFLD("ITMMASTER","ITMDES1",[M:POH0]ZSCNLN)
[M:POH2]ITMDES1(NOL) = func AFNC.INTFLD("ITMMASTER","ITMDES1",[M:POH0]ZSCNLN)
[M:POH2]PRHFCY(NOL) = [M:POH0]POHFCY
[M:POH2]FCYADD(NOL) = func AFNC.INTFLD("FACILITY","BPAADD",[M:POH0]POHFCY)
[M:POH2]LINSTOFCY(NOL) = func AFNC.INTFLD("BPARTNER","FCY",[M:POH0]BPSNUM)
[M:POH2]UOM(NOL) = func AFNC.INTFLD("ITMMASTER","STU",[M:POH0]ZSCNLN)
[M:POH2]QTYUOM(NOL) = VALEUR
[M:POH2]UOMPUUCOE(NOL) = VALEUR
[M:POH2]QTYSTU(NOL) = VALEUR
[M:POH2]EXTRCPDAT(NOL) = date$
[M:POH2]GROPRI = 0
[M:POH2]LINPURTYP = 1
[M:POH2]VAT1(NOL) = "NT"
[M:POH2]VAT2(NOL) = "NTX"
[M:POH2]VAT3(NOL) = ""
[M:POH2]QTYPUU(NOL) = 1
[M:POH2]PUU(NOL) = func AFNC.INTFLD("ITMMASTER","STU",[M:POH0]ZSCNLN)
[M:POH2]STU(NOL) = func AFNC.INTFLD("ITMMASTER","STU",[M:POH0]ZSCNLN)
[M:POH2]NETCUR(NOL) = "CAD"
[M:POH2]CPRCUR(NOL) = "CAD"
[M:POH2]LINWEU(NOL) = "KG"
[M:POH2]LINVOU(NOL) = "L"
[M:POH2]LINACC1(NOL) = "20200"
[M:POH2]LINCLEFLG(NOL) = 1
Goto GET_POSITION
End

$APPEND_LINE
[M:POH2]QTYUOM(NOL) += VALEUR
[M:POH2]QTYSTU(NOL) += VALEUR
[M:POH2]QTYPUU(NOL) += VALEUR
Goto GET_POSITION
End

$GET_POSITION
If max([M:POH2]NBLIG) = 0
    [M:POH2]NBLIG = 1
    Goto AFFZO_MASK
Else
    If max([M:POH2]NBLIG) > [L]NOL
        Goto AFFZO_MASK
    Else
        [M:POH2]NBLIG = max([M:POH2]NBLIG)+1
        Goto AFFZO_MASK
    Endif
Endif
End

$AFFZO_MASK
Affzo [POH2]1-900
Raz [M:POH0]ZSCNLN
Raz [M:POH0]ZSCNQY
Affzo [M:POH0]ZSCNLN
Affzo [M:POH0]ZSCNQY
Diszo [M:POH0]ZSCNQY
Actzo [M:POH0]ZSCNLN
zonsui="[M:POH0]ZSCNLN"
End

Subprog AM_BPSNUM(VALEUR)
Variable Char    VALEUR()
If GFLAG = "RET"
    If GREP = "C"
        If func AFNC.INTFLD("BPARTNER","BETFCY",VALEUR) = "2"
            Actzo [M:POH0]ZSCNLN
            Diszo [M:POH0]ZSCNQY
        Else
            Diszo [M:POH0]ZSCNLN
            Diszo [M:POH0]ZSCNQY
        Endif
    Else
        mkstat = 2
        GMESSAGE = 'Document does not exist.\Please click "NEW" first!'
    Endif
Endif
End

Subprog C_ZSCNQY(VALEUR)
Variable Decimal VALEUR
If VALEUR = 0
    Local Integer YN : YN = 2
    Call OUINON("Quantity must be greater than 0\Cancel the entry?", YN) From GESECRAN
    If YN = 2
        Goto AFFZO_MASK
    Else
        Raz [M:POH0]ZSCNQY
        Affzo [M:POH0]ZSCNQY
        zonsui="[M:POH0]ZSCNQY"
        mkstat=2
    Endif
Endif
If VALEUR > 1000
    GMESSAGE = "Maximum quantity 1000"
    Raz [M:POH0]ZSCNQY
    Affzo [M:POH0]ZSCNQY
    zonsui="[M:POH0]ZSCNQY"
    mkstat=2
Endif
End
